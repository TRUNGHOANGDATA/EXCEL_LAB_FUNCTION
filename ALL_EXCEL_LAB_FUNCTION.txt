CREATE_FILTER_VALIDATION_LIST=LET(
    HEADER, INDEX(DATA, 1, 0),
    DATA_ONLY, DROP(DATA, 1),
    FILTER_1, IF(
        FILTER_1_SELECTED = "",
        SEQUENCE(ROWS(DATA_ONLY)),
        CHOOSECOLS(DATA_ONLY, MATCH(COL_FILTER_NAME_1, HEADER, 0)) = FILTER_1_SELECTED
    ),
    FILTER_2, IF(
        FILTER_2_SELECTED = "",
        SEQUENCE(ROWS(DATA_ONLY)),
        CHOOSECOLS(DATA_ONLY, MATCH(COL_FILTER_NAME_2, HEADER, 0)) = FILTER_2_SELECTED
    ),
    FILTER_1_FINAL, IF(COL_RESULT = COL_FILTER_NAME_1, SEQUENCE(ROWS(FILTER_1)), FILTER_1),
    FILTER_2_FINAL, IF(COL_RESULT = COL_FILTER_NAME_2, SEQUENCE(ROWS(FILTER_2)), FILTER_2),
    UNIQUE(
        CHOOSECOLS(FILTER(DATA_ONLY, FILTER_1_FINAL * FILTER_2_FINAL), MATCH(COL_RESULT, HEADER, 0))
    )
);

CREATE_FILTER_VALIDATION_LIST_03_ITEMS=LET(
    HEADER, INDEX(DATA, 1, 0),
    DATA_ONLY, DROP(DATA, 1),
    FILTER_1, IF(
        FILTER_1_SELECTED = "",
        SEQUENCE(ROWS(DATA_ONLY)),
        CHOOSECOLS(DATA_ONLY, MATCH(COL_FILTER_NAME_1, HEADER, 0)) = FILTER_1_SELECTED
    ),
    FILTER_2, IF(
        FILTER_2_SELECTED = "",
        SEQUENCE(ROWS(DATA_ONLY)),
        CHOOSECOLS(DATA_ONLY, MATCH(COL_FILTER_NAME_2, HEADER, 0)) = FILTER_2_SELECTED
    ),
    FILTER_3, IF(
        FILTER_3_SELECTED = "",
        SEQUENCE(ROWS(DATA_ONLY)),
        CHOOSECOLS(DATA_ONLY, MATCH(COL_FILTER_NAME_3, HEADER, 0)) = FILTER_3_SELECTED
    ),
    FILTER_1_FINAL, IF(COL_RESULT = COL_FILTER_NAME_1, SEQUENCE(ROWS(FILTER_1)), FILTER_1),
    FILTER_2_FINAL, IF(COL_RESULT = COL_FILTER_NAME_2, SEQUENCE(ROWS(FILTER_2)), FILTER_2),
    FILTER_3_FINAL, IF(COL_RESULT = COL_FILTER_NAME_3, SEQUENCE(ROWS(FILTER_3)), FILTER_3),
    UNIQUE(
        CHOOSECOLS(
            FILTER(DATA_ONLY, FILTER_1_FINAL * FILTER_2_FINAL * FILTER_3_FINAL),
            MATCH(COL_RESULT, HEADER, 0)
        )
    )
);
